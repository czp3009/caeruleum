buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-gradle-plugin
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10'
    }
}

group = 'com.hiczp'
version = '2.0.0'
description = 'Retrofit inspired Http client base on ktor-client'

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

//kotlin
dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib'
    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-reflect
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect'
}
def jvm_target = JavaVersion.VERSION_1_8
def compiler_args = ['-Xjvm-default=all']
compileKotlin {
    kotlinOptions {
        jvmTarget = jvm_target
        freeCompilerArgs = compiler_args
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = jvm_target
        freeCompilerArgs = compiler_args
    }
}

//ktor
def ktor_version = '2.0.3'
dependencies {
    // https://mvnrepository.com/artifact/io.ktor/ktor-client-core
    implementation group: 'io.ktor', name: 'ktor-client-core', version: ktor_version
}

//test
dependencies {
    // https://mvnrepository.com/artifact/org.testng/testng
    testImplementation group: 'org.testng', name: 'testng', version: '7.6.1'
    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-test
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test'
    // https://mvnrepository.com/artifact/io.ktor/ktor-client-cio
    testImplementation group: 'io.ktor', name: 'ktor-client-cio', version: ktor_version
    // https://mvnrepository.com/artifact/io.ktor/ktor-client-content-negotiation
    testImplementation group: 'io.ktor', name: 'ktor-client-content-negotiation', version: ktor_version
    // https://mvnrepository.com/artifact/io.ktor/ktor-serialization-gson
    testImplementation group: 'io.ktor', name: 'ktor-serialization-gson', version: ktor_version
    // https://mvnrepository.com/artifact/io.ktor/ktor-client-logging
    testImplementation group: 'io.ktor', name: 'ktor-client-logging', version: ktor_version
    // https://mvnrepository.com/artifact/io.ktor/ktor-client-mock
    testImplementation group: 'io.ktor', name: 'ktor-client-mock', version: ktor_version
    // https://mvnrepository.com/artifact/com.github.salomonbrys.kotson/kotson
    testImplementation group: 'com.github.salomonbrys.kotson', name: 'kotson', version: '2.5.0'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-simple
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
}
test {
    useTestNG()
}

//publish
java {
    withJavadocJar()
    withSourcesJar()
}
publishing {
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties.ossUsername
                password = project.properties.ossPassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/czp3009/caeruleum'

                licenses {
                    license {
                        name = 'Apache License Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'czp3009'
                        name = 'czp3009'
                        email = 'czp3009@gmail.com'
                        url = 'https://www.hiczp.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/czp3009/caeruleum.git'
                    developerConnection = 'scm:git:ssh://github.com/czp3009/caeruleum.git'
                    url = 'https://github.com/czp3009/caeruleum'
                }
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}
gradle.taskGraph.whenReady { taskGraph ->
    tasks.signMavenJavaPublication.onlyIf { taskGraph.hasTask tasks.publish }
}
